/*

This widget will rotate a path around a point of origin. You can choose from one of the included Font Awesome paths
(circle notch, cog, pulse, spinner, or refresh) by setting the <iconStyle> property. Or you can assign your own path to the iconSVGPath property.
You specify the size of the path by setting the <iconSize> property. The path will always be centered in the middle of the widget.

You can change the amount of rotation that occurs as well as the frequency of animation by setting the
<rotateBy> and 'frequency' properties.

*/

widget community.livecode.paulmcclernan.knob

use com.livecode.canvas
use com.livecode.widget
use com.livecode.engine
use com.livecode.math
-- use community.livecode.trevordevore.widgetHelpers

metadata title is "Knob Widget"
metadata author is "Paul McClernan (with code from Trevor DeVore)"
metadata version is "1.0.0"
-- public
private variable mRotateBy          as Number
private variable mFrequency         as Number
private variable mSVGPath           as Path
private variable mColor             as Color
private variable mIconOrigin        as List
private variable mIconSize          as Number
private variable mIconStyle         as String
private variable mCurrentRotation   as Number

-- private
private variable mCenter            as Point
private variable mWorkingPath       as Path
private variable mRotation          as Number

property rotateBy           get mRotateBy            set setRotateBy
property currentRotation    get getRotation		     set setRotation  --- get mCurrentRotation
property frequency          get mFrequency           set mFrequency
property iconStyle          get mIconStyle           set setIconStyle
property iconSVGPath        get getIconPath          set setIconPath
property iconColor        	 get getColor             set setColor
property iconOrigin         get getIconOrigin        set setIconOrigin
property iconSize           get mIconSize            set setIconSize

metadata iconSVGPath.editor is "com.livecode.pi.text"
metadata iconColor.editor is "com.livecode.pi.color"
metadata iconColor.section is "Colors"
metadata iconStyle.editor is "com.livecode.pi.enum"
metadata iconStyle.options is "circle notch,knob1,cog"
-- metadata currentRotation.user_visible is "true"

private handler setIconSize(in pSize as Number)
	put pSize into mIconSize
	initializationCalculations()
	redraw all
end handler

private handler setRotation(in pRotation as Number)
	initializationCalculations()
	calculateRotatedPath(mWorkingPath, pRotation, mCenter, true)
	redraw all
end handler
	--   set the currentRotation of me to (the currentRotation of me)+10

private handler getRotation() returns Number
	-- log mCurrentRotation
	-- log mRotation
	return mCurrentRotation
end handler

private handler setRotateBy(in pRotateBy as Number)
	put pRotateBy into mRotateBy
	initializationCalculations()
	redraw all
end handler

private handler getIconPath() returns String
	return the instructions of mSVGPath
end handler

private handler setIconPath(in pPath as String)
	put path pPath into mSVGPath
	put "custom" into mIconStyle
	initializationCalculations()
	redraw all
end handler

private handler getColor() returns String
	return colorToString(mColor, false)
end handler

private handler setColor(in pColor as String)
	put stringToColor(pColor) into mColor
	redraw all
end handler

private handler getIconOrigin() returns String
	return (element 1 of mIconOrigin) formatted as string & "," & (element 2 of mIconOrigin) formatted as string
end handler

private handler setIconOrigin(in pPoint as String)
	variable tList as List
	split pPoint by "," into tList
	put tList parsed as list of number into tList
	if the number of elements in tList is 2 then
		put tList into mIconOrigin
		initializationCalculations()
		redraw all
	end if
end handler

private handler setIconStyle(in pStyle as String)
	_setIconStyle(pStyle)
	initializationCalculations()
	redraw all
end handler

private handler _setIconStyle(in pStyle as String)
	put 10 into mRotateBy
	put 0.01 into mFrequency
	put [0.50,0.50] into mIconOrigin
	put pStyle into mIconStyle


	if pStyle is "circle notch" then
		put path "M355.8,176.1c0,48.3-20.3,94.1-52,125.9s-77.6,52-125.9,52S83.8,333.8,52,302S0,224.4,0,176.1C0,117.4,28.6,63.7,75.1,30.8 c23.2-16.5,49-26.6,77.4-30.8v51.6C93.9,63.5,50.8,115.2,50.8,176.1c0,34.4,14.7,67.1,37.3,89.8c22.6,22.6,55.4,37.3,89.8,37.3 s67.3-14.7,90-37.3c22.6-22.6,37.1-55.4,37.1-89.8c0-61-43.1-112.6-101.7-124.5V0c57,8.1,105.6,42.9,132.4,93.7 C349.1,119.3,355.8,146.7,355.8,176.1z" into mSVGPath
	else if pStyle is "knob1" then
		put path "M118.13,16.67C104.64,5.35,88.22-0.18,71.88-0.18c-20.55,0-40.95,8.75-55.19,25.72C-8.87,56.01-4.9,101.42,25.56,126.98 c13.49,11.32,29.91,16.85,46.24,16.85c20.55,0,40.95-8.75,55.19-25.72C152.56,87.65,148.59,42.23,118.13,16.67z M8.23,77.39" & \
		"c-1.49-16.99,3.73-33.55,14.7-46.61C35.09,16.28,52.93,7.97,71.88,7.97h0c14.96,0,29.53,5.31,41.01,14.94 c13.07,10.96,21.08,26.36,22.57,43.35c1.49,16.99-3.73,33.55-14.7,46.61c-12.16,14.5-30.01,22.81-48.95,22.81" & \
		"c-10.77,0-21.34-2.76-30.69-7.9c0.68-0.14,1.34-0.35,2-0.58c9.02,4.69,18.88,7,28.7,7c17.8,0,35.48-7.58,47.82-22.28 c22.15-26.39,18.7-65.74-7.69-87.88c-11.69-9.81-25.91-14.6-40.06-14.6v1.39h0c14.29,0,28.2,5.07,39.17,14.27" & \
		"c12.48,10.47,20.14,25.18,21.56,41.41c1.42,16.23-3.57,32.04-14.04,44.52c-11.62,13.85-28.66,21.79-46.76,21.79 c-9.41,0-18.65-2.21-27-6.34c1.64-0.81,3.14-1.91,4.41-3.32c5.3-5.89,4.48-14.68-1.84-19.63c-2.8-2.19-6.2-3.26-9.59-3.26" & \
		"c-4.26,0-8.49,1.69-11.45,4.98c-1.14,1.27-1.98,2.67-2.56,4.14c-7.23-9.21-11.66-20.31-12.7-32.25 C9.66,60.91,14.65,45.1,25.12,32.62c11.62-13.85,28.66-21.79,46.76-21.79l0-1.39c0,0,0,0,0,0c-17.8,0-35.48,7.58-47.82,22.28" & \
		"c-19.49,23.23-19.15,56.5-0.79,79.24c-0.16,0.63-0.28,1.26-0.35,1.89C14.53,102.9,9.39,90.64,8.23,77.39z M25.83,115.17 c-0.26-2.92,0.64-5.77,2.53-8.02c2.09-2.49,5.16-3.93,8.42-3.93c2.57,0,5.08,0.91,7.06,2.57c4.64,3.89,5.25,10.84,1.35,15.48" & \
		"c-2.09,2.49-5.16,3.93-8.42,3.93c-2.57,0-5.08-0.91-7.06-2.57C27.47,120.74,26.09,118.09,25.83,115.17z M33.41,127.52 c1.3,0.37,2.63,0.57,3.97,0.6c10.61,6.51,22.54,9.72,34.43,9.72c18.84,0,37.55-8.02,50.61-23.58c23.44-27.93,19.79-69.57-8.14-93.01" & \
		"C101.91,10.87,86.86,5.81,71.88,5.81c-18.84,0-37.55,8.02-50.61,23.58c-21.51,25.63-20.19,62.8,1.75,86.86 c0.19,1.21,0.55,2.39,1.08,3.53C-0.54,95.26-2.74,55.42,20,28.32C32.89,12.96,51.8,4.14,71.88,4.14h0" & \
		 "c15.86,0,31.29,5.62,43.46,15.84c13.85,11.62,22.34,27.94,23.92,45.95c1.58,18.01-3.96,35.55-15.58,49.4 c-12.89,15.37-31.8,24.18-51.88,24.18C58.08,139.51,44.67,135.28,33.41,127.52z" into mSVGPath
	else if pStyle is "refresh" then
		put path "M181.2,15.1V68c0,4.1-3.4,7.6-7.6,7.6h-52.9c-4.1,0-7.6-3.4-7.6-7.6c0-2,0.7-3.8,2.2-5.3l16.3-16.3 c-11.7-10.7-25.4-16.2-41.2-16.2c-21.1,0-40.5,10.9-51.4,28.8c-0.8,1.3-2.9,5.9-6.3,13.8c-0.6,1.8-1.8,2.7-3.5,2.7H5.9 c-2,0-3.8-1.8-3.8-3.8v-0.8C12.4,28.8,47.3,0,90.6,0c23,0,45.5,9.1,62.4,25l15.3-15.2c1.5-1.5,3.3-2.2,5.3-2.2 C177.8,7.6,181.2,11,181.2,15.1z M178.3,109.5c0,0.4,0,0.7-0.1,0.8c-10,42.1-44.9,70.9-88,70.9c-23,0-45.2-9-62.1-25l-15.2,15.2 c-1.5,1.5-3.3,2.2-5.3,2.2c-4.1,0-7.6-3.4-7.6-7.6v-52.9c0-4.1,3.4-7.6,7.6-7.6h52.9c4.1,0,7.6,3.4,7.6,7.6c0,2-0.7,3.8-2.2,5.3 l-16.2,16.2C60.8,145.1,75.4,151,90.6,151c21.1,0,40.5-10.9,51.4-28.8c0.8-1.3,2.9-5.9,6.3-13.8c0.6-1.8,1.8-2.7,3.5-2.7h22.7 C176.5,105.7,178.3,107.5,178.3,109.5z" into mSVGPath
	else if pStyle is "cog" then
		put path "M322.8,138.5v46.7c0,3.4-2.5,6.9-5.9,7.6l-38.9,5.9c-2.7,7.6-5.5,13.9-8.2,19.1c4.8,6.9,12.4,16.6,22.5,29 c1.5,1.7,2.1,3.4,2.1,5.3c0,1.9-0.6,3.4-1.9,4.8c-7.6,10.3-34.5,37.6-40.6,37.6c-1.7,0-3.6-0.6-5.5-1.9l-29-22.7 c-6.1,3.2-12.6,5.9-19.1,8c-2.3,19.1-4.2,32.2-6.1,39.1c-1.1,4-3.6,5.9-7.6,5.9h-46.7c-4,0-7.4-2.7-7.6-6.3l-5.9-38.7 c-6.9-2.3-13.2-4.8-18.9-7.8l-29.6,22.5c-1.5,1.3-3.2,1.9-5.3,1.9c-1.9,0-3.8-0.8-5.3-2.3c-17.7-16-29.2-27.7-34.7-35.3 c-1.1-1.5-1.5-2.9-1.5-4.8c0-1.7,0.6-3.4,1.7-4.8c2.1-2.9,5.7-7.6,10.7-14.1c5-6.3,8.8-11.1,11.3-14.7c-3.8-6.9-6.7-13.9-8.6-20.8 l-38.5-5.7c-3.6-0.6-6.1-4-6.1-7.6v-46.7c0-3.4,2.5-6.9,5.7-7.6l39.1-5.9c1.9-6.5,4.6-12.8,8.2-19.3c-5.7-8-13-17.7-22.5-29 c-1.5-1.7-2.1-3.4-2.1-5c0-1.5,0.6-2.9,1.9-4.8c7.4-10.1,34.5-37.6,40.6-37.6c1.9,0,3.6,0.6,5.5,2.1l29,22.5 c6.1-3.2,12.6-5.9,19.1-8c2.3-19.1,4.2-32.2,6.1-39.1c1.1-4,3.6-5.9,7.6-5.9h46.7c4,0,7.4,2.7,7.6,6.3l5.9,38.7 c6.9,2.3,13.2,4.8,18.9,7.8L247,30.3c1.3-1.3,2.9-1.9,5-1.9c1.9,0,3.6,0.6,5.3,2.1c18.1,16.6,29.6,28.6,34.7,35.7 c1.1,1.1,1.5,2.7,1.5,4.6c0,1.7-0.6,3.4-1.7,4.8c-2.1,2.9-5.7,7.6-10.7,13.9c-5,6.5-8.8,11.3-11.3,14.9c3.6,6.9,6.5,13.9,8.6,20.6 l38.5,5.9C320.3,131.6,322.8,134.9,322.8,138.5z M199.5,199.5c10.5-10.5,15.8-23.1,15.8-38c0-29.6-24.2-53.8-53.8-53.8 s-53.8,24.2-53.8,53.8s24.2,53.8,53.8,53.8C176.3,215.2,189,210,199.5,199.5z" into mSVGPath
	end if
end handler

public handler OnCreate()
	_setIconStyle("circle notch")
	put color [0/255, 0/255, 0/255] into mColor
	put [0.50,0.50] into mIconOrigin
	put 80 into mIconSize
	put 90 into mRotation
end handler

public handler OnSave(out rProperties as Array)
		put the empty array into rProperties
		put colorToString(mColor, true) into rProperties["color"]
		put mRotateBy into rProperties["rotateBy"]
		put mFrequency into rProperties["frequency"]
		put the instructions of mSVGPath into rProperties["svgPath"]
		put mIconOrigin into rProperties["iconOrigin"]
		put mIconSize into rProperties["iconSize"]
		put mIconStyle into rProperties["iconStyle"]
		put mCurrentRotation into rProperties["currentRotation"]
end handler

public handler OnLoad(in pProperties as Array)
	put path pProperties["svgPath"] into mSVGPath
	put stringToColor(pProperties["color"]) into mColor
	put pProperties["rotateBy"] into mRotateBy
	put pProperties["frequency"] into mFrequency
	put pProperties["iconOrigin"] into mIconOrigin
	put pProperties["iconSize"] into mIconSize
	put pProperties["iconStyle"] into mIconStyle
	put pProperties["currentRotation"] into mCurrentRotation
end handler


	public handler OnOpen()
		initializationCalculations()
		-- schedule timer in mFrequency seconds
	end handler


	public handler OnGeometryChanged()
		initializationCalculations()
	end handler

-- this message doesn't seem to be firing yet
-- public handler OnVisibilityChanged(in pVisible as Boolean)
-- end handler

	public handler OnMouseUp()
		if the mouse position is within my bounds then
			post "mouseUp" with [the click button] -- to my script object
		else
			post "mouseRelease" with [the click button] -- formatint needed until dp-2
		end if
		redraw all
	end handler

	public handler OnMouseMove()
		post "mouseMove" to my script object -- to my script object
		redraw all
	end handler

	public handler OnMouseCancel()
		post "mouseRelease" to my script object with [the click button] -- to my script object
	end handler


	public handler OnMouseDown()
		post "mouseDown" to my script object with [the click button] -- to my script object
		redraw all
	end handler

	private handler initializationCalculations()
		-- Always start with the shape at original position.
		put mSVGPath into mWorkingPath

		put preparePathForRotation(mWorkingPath, mIconOrigin, rectangle [0,0,mIconSize,mIconSize], my rectangle) into mCenter

		put 0 into mRotation
	end handler

	----------
	-- called whenever LiveCode needs to redraw the widget
	public handler OnPaint()
		set the paint of this canvas to solid paint with mColor
		fill mWorkingPath on this canvas
	end handler


	public handler OnClose()
		cancel timer
	end handler

	----------
	-- this handler is called when the timer scheduled with 'schedule timer' fires
	--public handler OnTimer()
	--end handler
	----------

	-- this handler returns the amount of time until the seconds change
	--handler computeNextTimer() returns Number
		-- use the 'execute script' command
		--execute script "return 1 - (the long seconds - round(the long seconds - 0.5))"
		--return the result
	--end handler


	-- Translated from some Skia code
	public handler scaleAndMaintainAspectRatioTransform(in pSrcBounds as Rectangle, in pDestBounds as Rectangle, in pConstraintBounds as Rectangle) returns Transform
		// Prepare values for matrix transformations
		variable isLarger as Boolean
		variable sX as Number
		variable sY as Number

		put false into isLarger
		put the width of pDestBounds / the width of pSrcBounds into sX
		put the height of pDestBounds / the height of pSrcBounds into sY

		if sX > sY then
			put true into isLarger
			put sY into sX
		else
			put sX into sY
		end if

		variable tX as Number
		variable tY as Number

		put the left of pDestBounds - (the left of pSrcBounds*sX) into tX
		put the top of pDestBounds - (the top of pSrcBounds*sY) into tY

		variable tDiff as Number

		if isLarger then
			put the width of pConstraintBounds - (the width of pSrcBounds*sY) into tDiff
		else
			put the height of pConstraintBounds - (the height of pSrcBounds*sY) into tDiff
		end if

		// align center
		divide tDiff by 2

		if isLarger then
			add tDiff to tX
		else
			add tDiff to tY
		end if

		// create transformation matrix and apply
		variable tTransform as Transform
		put transform with matrix [sX, 0, 0, sY, tX, tY] into tTransform

		return tTransform
	end handler

	-- Needed until dp-2
	handler FormatInt(in pNumber as Number) returns String
		variable tNumberString as String

		put pNumber formatted as string into tNumberString

		if "." is in tNumberString then
			variable tDotOffset
			put the first offset of "." in tNumberString into tDotOffset
			delete char tDotOffset to (the number of chars in tNumberString) of tNumberString
		end if

		return tNumberString
	end handler

	private handler colorToString(in pColor as Color, in pIncludeAlpha as Boolean) returns String
		variable tColor as String

		put FormatInt(the rounded of ((the red of pColor) * 255)) into tColor
		put "," & FormatInt(the rounded of ((the green of pColor) * 255)) after tColor
		put "," & FormatInt(the rounded of ((the blue of pColor) * 255)) after tColor

		if pIncludeAlpha then
			put "," & FormatInt(the rounded of ((the alpha of pColor) * 255)) after tColor
		end if

		return tColor
	end handler

	----------
	-- this handler converts a String of numbers to an RGBA color
	private handler stringToColor(in pString as String) returns Color
		variable tRed as Real
		variable tGreen as Real
		variable tBlue as Real
		variable tAlpha as Real

		variable tComponentList as List
		split pString by "," into tComponentList

		variable tComponentCount
		put the number of elements in tComponentList into tComponentCount
		if tComponentCount is not 3 and tComponentCount is not 4 then
			// Invalid number of components detected
			throw "Invalid color"
		end if

		put (element 1 of tComponentList) parsed as number into tRed
		put (element 2 of tComponentList) parsed as number into tGreen
		put (element 3 of tComponentList) parsed as number into tBlue

		if tComponentCount is 4 then
			put (element 4 of tComponentList) parsed as number into tAlpha
		else
			put 255 into tAlpha
		end if

		return color [ tRed/255, tGreen/255, tBlue/255, tAlpha/255 ]
	end handler

	-- Very useful article: http://docs.rainmeter.net/tips/transformation-matrix-guide
   public handler calculateRotatedPath(inout pPath as Path, in pRotation as Number, in pCenter as Point, in pRotateClockwise as Boolean)
      variable tTransform as Transform
      put rotatedPathTransformation(pRotation, pCenter, pRotateClockwise) into tTransform
      transform pPath by tTransform
   end handler


	  public handler rotatedPathTransformation(in pRotation as Number, in pCenter as Point, in pRotateClockwise as Boolean) returns Transform
	     variable tX as Number
	     variable tY as Number
	     variable tOriginX as Number
	     variable tOriginY as Number
	     variable tMatrix as List
	     variable tA as Number
	     variable tB as Number
	     variable tC as Number
	     variable tD as Number

	     -- Clockwise: sin for B, -sin for C
	     -- Counterclockwise: -sin for B, sin for C
	     variable tAngle as Number
	     put (pRotation mod 360)/360*2*pi into tAngle

	     put cos(tAngle) into tA -- scale the x

	     if pRotateClockwise then
	        put sin(tAngle) into tB -- scale the y
	        put -sin(tAngle) into tC -- skew the x
	     else
	        put -sin(tAngle) into tB -- scale the y
	        put sin(tAngle) into tC -- skew the x
	     end if
	     put cos(tAngle) into tD -- skew the y
	     put the x of pCenter into tOriginX
	     put the y of pCenter into tOriginY

	     put tOriginX - tOriginX*tA - tOriginY*tC into tX
	     put tOriginY - tOriginX*tB - tOriginY*tD into tY

	     variable tTransform as Transform
	     put transform with matrix [tA,tB,tC,tD,tX,tY] into tTransform

	     return tTransform
	  end handler


	  -- mIconOrigin Element 1 is percent of icon width that represents center X (e.g. 0.5). Element 2 is percent of icon height that represents center Y (e.g. 0.5).
	  -- pDestBounds Rectangle to constrain the path to.
	  -- pConstraintBounds The rect to center the path in.
	  public handler preparePathForRotation(inout xPath as Path, in pIconOrgin as List, in pDestBounds as Rectangle, in pConstraintBounds as Rectangle) returns Point
	     transform xPath by scaleAndMaintainAspectRatioTransform(the bounding box of xPath, pDestBounds, pConstraintBounds)
	     -- Reset to 0,0
	     translate xPath by [- the x of the bounding box of xPath, \
	                                   - the y of the bounding box of xPath]

	     -- Find center point to rotate around and center path
	     variable tCenterX as Number
	     variable tCenterY as Number

	     put (the width of the bounding box of xPath * element 1 of pIconOrgin) into tCenterX
	     put (the height of the bounding box of xPath * element 2 of pIconOrgin) into tCenterY
	     translate xPath by [the width of pConstraintBounds/2-tCenterX, the height of pConstraintBounds/2-tCenterY]
	     return point [(the left of the bounding box of xPath) + tCenterX, \
	                       (the top of the bounding box of xPath) + tCenterY]
	  end handler

end widget
